// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16],inc, load, reset;
    OUT out[16];
    
    PARTS:
    
    /*
    reset = 1: 
        0
    reset = 0:
        load = 1:
            in
        load = 0:
            inc = 1:
                out + 1
            inc = 0
                out
        
    */
    
    Inc16(
        in = regout,
        out = incout
    );
    
    Mux16(
        a = regout,
        b = incout, 
        sel = inc,
        out = outOrInc
    );
    
    Mux16(
        a = outOrInc,
        b = in, 
        sel = load,
        out = outOrIncOrLoad
    );
    
    Mux16(
        a = outOrIncOrLoad,
        b = false, 
        sel = reset,
        out = regin
    );    
    
    
    Register(
        in = regin,
        load = true,
        out = regout,
        out = out
    );
}
